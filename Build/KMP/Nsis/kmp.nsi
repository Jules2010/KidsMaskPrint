; Script generated by the HM NIS Edit Script Wizard.

; HM NIS Edit Wizard helper defines
!define PRODUCT_NAME "Kids Mask Print"
!define PRODUCT_VERSION "1.0.4"
!define PRODUCT_PUBLISHER "Mindwarp Consultancy Ltd"
!define PRODUCT_WEB_SITE "http://www.example.com"
!define PRODUCT_DIR_REGKEY "Software\Microsoft\Windows\CurrentVersion\KidsMaskPrint\KidsMaskPrint.exe"
!define PRODUCT_UNINST_KEY "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_NAME}"
!define PRODUCT_UNINST_ROOT_KEY "HKLM"

; MUI 1.67 compatible ------
!include "MUI.nsh"

; MUI Settings
!define MUI_ABORTWARNING
!define MUI_ICON "${NSISDIR}\Contrib\Graphics\Icons\modern-install.ico"
!define MUI_UNICON "${NSISDIR}\Contrib\Graphics\Icons\modern-uninstall.ico"

; Welcome page
!insertmacro MUI_PAGE_WELCOME
; License page
!insertmacro MUI_PAGE_LICENSE "..\Distrib\EULA.txt"
; Directory page
!insertmacro MUI_PAGE_DIRECTORY
; Instfiles page
!insertmacro MUI_PAGE_INSTFILES
; Finish page
!define MUI_FINISHPAGE_RUN "$INSTDIR\KidsMaskPrint.exe"
!insertmacro MUI_PAGE_FINISH

; Uninstaller pages
!insertmacro MUI_UNPAGE_INSTFILES

; Language files
!insertmacro MUI_LANGUAGE "English"

; MUI end ------

Name "${PRODUCT_NAME} ${PRODUCT_VERSION}"
OutFile "kmpinst.exe"
InstallDir "$PROGRAMFILES\${PRODUCT_PUBLISHER}\KidsMaskPrint"
InstallDirRegKey HKLM "${PRODUCT_DIR_REGKEY}" ""
ShowInstDetails show
ShowUnInstDetails show

Section "MainSection" SEC01
  SetOutPath "$INSTDIR"
  SetOverwrite on
  File "..\AppBasic.dll"
  File "..\Beside02.exe"
  File "..\Distrib\Beside02.exe.config"
  File "..\Beside03.exe"
  File "..\Distrib\Beside03.exe.config"
  File "..\KidsMaskPrint.exe"
  File "..\Distrib\KidsMaskPrint.exe.config"
  File "..\..\..\HelpBuild\KidsMaskPrint\KidsMaskPrint.chm"
  CreateDirectory "$SMPROGRAMS\KidsMaskPrint"
  CreateShortCut "$SMPROGRAMS\KidsMaskPrint\KidsMaskPrint.lnk" "$INSTDIR\KidsMaskPrint.exe"
  CreateShortCut "$DESKTOP\KidsMaskPrint.lnk" "$INSTDIR\KidsMaskPrint.exe"
  File "..\MCLCore.dll"
  File "..\ProbHand.dll"
  File "..\SharpZipLib.dll"
  File "..\WinOnly.dll"
  File "..\UIStyle.dll"
  File "..\Distrib\Clown.mask"
  SetOutPath "$INSTDIR\FaceParts"
  File "..\FaceParts\Basic.dat"
  File "..\FaceParts\Halloween.dat"
  File "..\FaceParts\Jungle.dat"
  SetOutPath "$INSTDIR"
  File "..\Distrib\KidsMaskPrint.exe.manifest"
SectionEnd

Section -AdditionalIcons
  WriteIniStr "$INSTDIR\${PRODUCT_NAME}.url" "InternetShortcut" "URL" "${PRODUCT_WEB_SITE}"
  CreateShortCut "$SMPROGRAMS\KidsMaskPrint\Website.lnk" "$INSTDIR\${PRODUCT_NAME}.url"
SectionEnd

Section -Post
  WriteUninstaller "$INSTDIR\uninst.exe"
  WriteRegStr HKLM "${PRODUCT_DIR_REGKEY}" "" "$INSTDIR\KidsMaskPrint.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayName" "$(^Name)"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "UninstallString" "$INSTDIR\uninst.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayIcon" "$INSTDIR\KidsMaskPrint.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayVersion" "${PRODUCT_VERSION}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "URLInfoAbout" "${PRODUCT_WEB_SITE}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "Publisher" "${PRODUCT_PUBLISHER}"
SectionEnd


Function un.onUninstSuccess
  HideWindow
  MessageBox MB_ICONINFORMATION|MB_OK "$(^Name) was successfully removed from your computer."
FunctionEnd

Function un.onInit
  MessageBox MB_ICONQUESTION|MB_YESNO|MB_DEFBUTTON2 "Are you sure you want to completely remove $(^Name) and all of its components?" IDYES +2
  Abort
FunctionEnd

Section Uninstall
  ExecWait '"$INSTDIR\Beside03.exe" KMP'
  
  Delete "$INSTDIR\${PRODUCT_NAME}.url"
  Delete "$INSTDIR\uninst.exe"
  Delete "$INSTDIR\KidsMaskPrint.exe.manifest"
  Delete "$INSTDIR\KidsMaskPrint.exe.config"
  Delete "$INSTDIR\FaceParts\Basic.dat"
  Delete "$INSTDIR\FaceParts\Halloween.dat"
  Delete "$INSTDIR\FaceParts\Jungle.dat"
  Delete "$INSTDIR\UIStyle.dll"
  Delete "$INSTDIR\WinOnly.dll"
  Delete "$INSTDIR\Clown.mask"
  Delete "$INSTDIR\SharpZipLib.dll"
  Delete "$INSTDIR\ProbHand.dll"
  Delete "$INSTDIR\MCLCore.dll"
  Delete "$INSTDIR\KidsMaskPrint.exe"
  Delete "$INSTDIR\Beside03.exe"
  Delete "$INSTDIR\Beside03.exe.config"
  Delete "$INSTDIR\Beside02.exe"
  Delete "$INSTDIR\Beside02.exe.config"
  Delete "$INSTDIR\AppBasic.dll"
  Delete "$INSTDIR\KidsMaskPrint.chm"
  Delete "$SMPROGRAMS\KidsMaskPrint\Website.lnk"
  Delete "$DESKTOP\KidsMaskPrint.lnk"
  Delete "$SMPROGRAMS\KidsMaskPrint\KidsMaskPrint.lnk"

  RMDir "$SMPROGRAMS\KidsMaskPrint"
  RMDir "$INSTDIR\FaceParts"
  RMDir "$INSTDIR"

  DeleteRegKey ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}"
  DeleteRegKey HKLM "${PRODUCT_DIR_REGKEY}"
  SetAutoClose true
SectionEnd



Function .onInit

	;checkCommandLine:
		Call GetParameters
		Pop $R0

	;check95:
		Call GetWindowsVersion
		Pop $R0
		;MessageBox MB_OK "Your Operating System is $R0"
		StrCmp $R0 "95" abort

	;checkNET11:
		Call GetFramework10
		Return

	abort:
		Abort "Kids Mask Print can not run on Windows 95 Operating System"

FunctionEnd


; Usage:
;   Call GetWindowsVersion
;   Pop $R0
;
; at this point $R0 is "NT 4.0" or whatnot
Function GetWindowsVersion


  Push $R0
  Push $R1

  ClearErrors

  ReadRegStr $R0 HKLM \
  "SOFTWARE\Microsoft\Windows NT\CurrentVersion" CurrentVersion

  IfErrors 0 lbl_winnt

  ; we are not NT
  ReadRegStr $R0 HKLM \
  "SOFTWARE\Microsoft\Windows\CurrentVersion" VersionNumber

  StrCpy $R1 $R0 1
  StrCmp $R1 '4' 0 lbl_error

  StrCpy $R1 $R0 3

  StrCmp $R1 '4.0' lbl_win32_95
  StrCmp $R1 '4.9' lbl_win32_ME lbl_win32_98

  lbl_win32_95:
    StrCpy $R0 '95'
  Goto lbl_done

  lbl_win32_98:
;;beginning of additions to support win 98 SE
    push $R0
    push "."
    call StrStr
    pop $R0
    StrCpy $R0 $R0 "" 1
    StrCmp $R0 "10.2222" lbl_win32_98SE
    StrCpy $R0 '98'  ;;this line was not added
  Goto lbl_done      ;;this line was not added either

  lbl_win32_98SE:
    StrCpy $R0 '98 SE'
  Goto lbl_done
;;end of additions to support win 98 SE
  lbl_win32_ME:
    StrCpy $R0 'ME'
  Goto lbl_done

  lbl_winnt:

  StrCpy $R1 $R0 1

  StrCmp $R1 '3' lbl_winnt_x
  StrCmp $R1 '4' lbl_winnt_x

  StrCpy $R1 $R0 3

  StrCmp $R1 '5.0' lbl_winnt_2000
  StrCmp $R1 '5.1' lbl_winnt_XP
  StrCmp $R1 '5.2' lbl_winnt_2003 lbl_error

  lbl_winnt_x:
    StrCpy $R0 "NT $R0" 6
  Goto lbl_done

  lbl_winnt_2000:
    StrCpy $R0 '2000'
  Goto lbl_done

  lbl_winnt_XP:
    StrCpy $R0 'XP'
  Goto lbl_done

  lbl_winnt_2003:
    StrCpy $R0 '2003'
  Goto lbl_done

  lbl_error:
    StrCpy $R0 ''
  lbl_done:

  Pop $R1
  Exch $R0

FunctionEnd


; StrStr
; input, top of stack = string to search for
;        top of stack-1 = string to search in
; output, top of stack (replaces with the portion of the string remaining)
; modifies no other variables.
;
; Usage:
;   Push "this is a long ass string"
;   Push "ass"
;   Call StrStr
;   Pop $R0
;  ($R0 at this point is "ass string")

Function StrStr
   Exch $R1 ; st=haystack,old$R1, $R1=needle
   Exch    ; st=old$R1,haystack
   Exch $R2 ; st=old$R1,old$R2, $R2=haystack
   Push $R3
   Push $R4
   Push $R5
   StrLen $R3 $R1
   StrCpy $R4 0
   ; $R1=needle
   ; $R2=haystack
   ; $R3=len(needle)
   ; $R4=cnt
   ; $R5=tmp
   loop:
     StrCpy $R5 $R2 $R3 $R4
     StrCmp $R5 $R1 done
     StrCmp $R5 "" done
     IntOp $R4 $R4 + 1
     Goto loop
done:
   StrCpy $R1 $R2 "" $R4
   Pop $R5
   Pop $R4
   Pop $R3
   Pop $R2
   Exch $R1
FunctionEnd

Function GetFramework10

	ReadRegStr $R0 HKLM "SOFTWARE\Microsoft\.NETFramework" "InstallRoot"

	StrCmp $R0 "" NoFramework VerifyVersion

	NoFramework:
		MessageBox MB_YESNO|MB_ICONEXCLAMATION "The installer can not find Microsoft .NET Framework on this computer!$\r$\nPlease install .NET and run this installer again.$\r$\nYou can download and install .NET Framework through Microsoft Windows Update service. If you have Windows XP, this download can be found on your installation CD! Would you like to open Windows Update now?" IDNO abortNET
		ExecShell open "http://www.windowsupdate.com"
		Quit

	VerifyVersion:
		IfFileExists "$R0\v1.0.3705\*.*" VersionFound VersionNotFound

	VersionNotFound:
	  MessageBox MB_YESNO|MB_ICONEXCLAMATION "Kids Mask Print requires the .NET Framework version 1.0 or superior to run.$\r$\nPlease update .NET Framework and run this installer again.$\r$\nYou can update .NET Framework through Microsoft Windows Update service. Would you like to open Windows Update now?" IDNO abortNET
		ExecShell open "http://www.windowsupdate.com"
		Quit

	abortNET:
		Abort "No .NET 1.0 found"

	VersionFound:

FunctionEnd

; GetParameters
; input, none
; output, top of stack (replaces, with e.g. whatever)
; modifies no other variables.
Function GetParameters

   Push $R0
   Push $R1
   Push $R2
   Push $R3

   StrCpy $R2 1
   StrLen $R3 $CMDLINE

   ;Check for quote or space
   StrCpy $R0 $CMDLINE $R2
   StrCmp $R0 '"' 0 +3
     StrCpy $R1 '"'
     Goto loop
   StrCpy $R1 " "

   loop:
     IntOp $R2 $R2 + 1
     StrCpy $R0 $CMDLINE 1 $R2
     StrCmp $R0 $R1 get
     StrCmp $R2 $R3 get
     Goto loop

   get:
     IntOp $R2 $R2 + 1
     StrCpy $R0 $CMDLINE 1 $R2
     StrCmp $R0 " " get
     StrCpy $R0 $CMDLINE "" $R2

   Pop $R3
   Pop $R2
   Pop $R1
   Exch $R0

FunctionEnd

; eof
